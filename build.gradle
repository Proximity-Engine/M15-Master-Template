plugins {
    id 'proximity.java-conventions'
    id 'extra-java-module-info'
    id 'java'
    id 'maven-publish'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'dev.hephaestus.proximity'
version '1.1.0-SNAPSHOT'

javafx {
    version = "19"
    modules = [ 'javafx.controls' ]
}

repositories {
    mavenCentral()
    maven {
        name = 'Quilt'
        url = 'https://maven.quiltmc.org/repository/release'
    }
    mavenLocal()
    maven {
        url = "https://hephaestus.dev/snapshot"
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    implementation 'dev.hephaestus.proximity:application:1.2.0-SNAPSHOT'
    implementation 'dev.hephaestus.proximity:mtg:1.2.0-SNAPSHOT'
    implementation 'dev.hephaestus.proximity:scryfall:1.2.0-SNAPSHOT'
    implementation 'dev.hephaestus.proximity:json:1.0.0-SNAPSHOT'
    implementation 'org.quiltmc:quilt-json5:1.0.0'
}

extraJavaModuleInfo {
    // This does not have to be a complete description (e.g. here 'org.apache.commons.collections' does not export anything here).
    // It only needs to be good enough to work in the context of this application we are building.
    module('quilt-json5-1.0.0.jar', 'org.quiltmc.quilt_json5', '1.0.0') {
        exports('org.quiltmc.json5')
    }
    module('j2objc-annotations-1.3.jar', 'com.google.j2objc.j2objc-annotations', '1.3') {
    }
    module('failureaccess-1.0.1.jar', 'com.google.guava.failureaccess', '1.0.1') {
    }
    module('jsr305-3.0.2.jar', 'com.google.code.findbugs.jsr305', '3.0.2') {
    }
    module('listenablefuture-9999.0-empty-to-avoid-conflict-with-guava.jar', 'com.google.guava.listenablefuture', '9999.0-empty-to-avoid-conflict-with-guava') {
    }
}


task run(dependsOn: configurations.compileClasspath, type: JavaExec) {
    group = "proximity"
    classpath = sourceSets.test.runtimeClasspath
    mainModule.set('dev.hephaestus.proximity.app')
    mainClass.set('dev.hephaestus.proximity.app.impl.Main')
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}

sourceSets {
    main {
        output.resourcesDir = "build/classes/java/main"
    }
    test {
        output.resourcesDir = "build/classes/java/test"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}